plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.jhssong'
version = System.getenv("GITHUB_REF_NAME")?.startsWith("v")
        ? System.getenv("GITHUB_REF_NAME").substring(1)
        : "0.0.0-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "com.jhssong"
            artifactId = "errorping"
            version = project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/jhssong/errorping")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


dependencies {
    // Spring Boot BOM
    api platform("org.springframework.boot:spring-boot-dependencies:3.5.4")
    annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:3.5.4")
    testAnnotationProcessor platform("org.springframework.boot:spring-boot-dependencies:3.5.4")

    // Spring
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-webflux'
    compileOnly 'org.springframework:spring-webmvc'
    compileOnly 'org.springframework:spring-context'


    // Spring Boot AutoConfiguration
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Spring Security
    compileOnly 'org.springframework.boot:spring-boot-starter-security'

    // Jakarta
    compileOnly 'jakarta.servlet:jakarta.servlet-api'
    compileOnly 'jakarta.validation:jakarta.validation-api'
    testImplementation 'jakarta.servlet:jakarta.servlet-api'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Jackson
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.core:jackson-annotations'

    // SLF4J
    api 'org.slf4j:slf4j-api'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-webmvc'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'ch.qos.logback:logback-classic'
}


test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        events("passed", "skipped", "failed", "standardOut", "standardError")
    }
}
